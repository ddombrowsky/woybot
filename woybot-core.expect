#!/usr/bin/expect -df
#
# NetHack automated player
#
# Maybe someday it will do the extinctionist game I seek.
#

set timeout 5
set match_max 20000

set user woybot
set pass U5ZjIwYzc5OTMyNz

set islocal 0
if { [lindex $argv 0] == "-l" } {
    set islocal 1
}
if { $islocal == 1 } {
    spawn nethack -u woybot
} else {
    spawn telnet nethack.csh.rit.edu

    sleep 1

    expect {
        timeout {
            send_user "can't connect, must bail\n\n"
            exit
        }
        "=>"
    }

    send "l$user\r$pass\r"

    sleep 1

    expect {
        timeout exit
        "Logged in as: $user"
    }

    send "p"

    sleep 1
}

expect {
    timeout exit
    "There are some stale" {
        sleep 10
        exp_continue
    }
    "Shall I pick a character's race" {
        # wizard human male neutral
        send "nwhmn"
        exp_continue
    }
    -re "--More--" {
        send " "
        exp_continue
    }
    "to NetHack"
}

# 0.1 sec delay every 1 char
set send_slow {1 0.3}

send -s ";.v"
expect {
    timeout exit
    -re "--More--" {
        send " ;.v"
        exp_continue
    }
    "Unix NetHack"
}

#
# procs
#

proc interested {} {
    return ""
}

#
# explore
#

#set dirs [list l j y j u h n h k l b]
set dirs [list l j h k h k l j l j y j u k n h k l b]

set doauto 1
set idle_count 0
set isdead 0
while {$doauto == 1} {
    foreach dir $dirs {
        send_error "XXX DIRECTION: $dir\n"
        set cont 1
        while {$cont == 1} {
            # identify the next square
            send ";$dir." 
            set timeout 0.5
            expect {
                timeout {
                    set cont 0
                    incr idle_count
                }
                "+       a spellbook or a closed door (closed door)" {
                    send -s "o$dir"
                    sleep 0.25
                    expect {
                        "This door is locked." {
                            send \004
                            send "$dir"
                        }
                        -re "The door opens|The door resists!" {
                            send "$dir"
                            set cont 1
                            set idle_count 0
                        }
                    }
                }
                ".       a doorway or the floor of a room" {
                    send "$dir"
                    set cont 1
                    set idle_count 0
                }
                -gl "-       a wall or an open door (open door)" {
                    # doors can only be entered orthogonally
                    if { [lsearch [list l k j h ] $dir ] >= 0 } {
                        send "$dir"
                        set cont 1
                        set idle_count 0
                    } else {
                        set cont 0
                    }
                }
                -re "#       iron bars or a tree.*\\(corridor\\)" {
                    # maybe sometimes travel down corridors?
                    #send [string toupper "$dir"]
                    send "G$dir"
                    set cont 1
                    set idle_count 0
                }
                -re "(tame \\w+ called Mountain Lion Cat)" {
                    send "m$dir"
                    set cont 1
                    set idle_count 0
                }
                "<       a staircase up or a ladder up (staircase up)" {
                    send "$dir"
                    set cont 1
                    set idle_count 0
                }
                ">       a staircase up or a ladder down (staircase down)" {
                    send "$dir"
                    set cont 1
                    set idle_count 0
                }
                -re "%       a piece of food \\((\[^)\]+)\\)" {
                    if { $expect_out(1,string) == "corpse" } {
                        send_error "XXX: not eating corpse!\n"
                        send "$dir"
                    } elseif { $expect_out(1,string) == "tin" } {
                        send_error "XXX: not eating tin!\n"
                        send "$dir"
                    } else {
                        send_error "XXX: eating (found food)\n"
                        send "${dir}ey"
                    }
                }
                -re "\\(       a useful item.*\\(large box\\)" {
                    send "$dir"
                    set cont 1
                    set idle_count 0

                }
                -re "Weak|Fainting" {
                    # place a call!
                    #send "#py"
                    send "#pray\ry"
                    sleep 0.1
                    expect {
                        -timeout 3
                        "You feel that" {
                            send_error "XXX: done with the call\n"
                        }
                        "angered" {
                            # uh oh...
                            sleep 2
                            exp_continue
                        }
                        -re "You fry to a crisp|You die\\." {
                            send_error "XXX: and the false god hath made us DEAD again\n"
                            set isdead 1
                            set cont 0
                            set doauto 0
                        }
                        -gl "--More--" {
                            send " "
                            sleep 1
                            exp_continue
                        }
                    }
                }
                "\$       " {
                    # pick up cash
                    send "$dir,"
                    expect {
                        "(end)" {
                            # multiple items, choose only gold
                            send "\$ "
                            exp_continue
                        }
                        "gold pieces"
                    }
                    set idle_count 0
                }
                -re ":       |d       |x       |r       |F       " {
                    set monst "UNKNOWN"
                    # attack!
                    send_error "XXX: attack!"
                    send -s "$dir"
                    sleep 0.1
                    expect {
                        -re "You kill the" {
                            send "${dir}"
                            exp_continue
                        }
                        "You see here a (.*) corpse" {
                            set monst $expect_out(0,string)

                            # eat the corpse
                            send_error "XXX: eating ($monst corpse)\n"
                            send "ey"
                        }
                    }
                    set cont 1
                    set idle_count 0
                }
                -re "You die\\." {
                    send_error "XXX: well look at that, DEAD again\n"
                    sleep 1
                    set isdead 1
                }

                # "--More--" must come last, to avoid losing
                # anything that came before it "e.g. "You die..."
                -gl "--More--" {
                    send " "
                    set cont 1
                }
            }

            set ndir [interested]
            if { $ndir != "" } {
                send_error "XXX: interested in direction $ndir"
                set dir $ndir
            }

            if { $idle_count == 0 } {
                # uncomment for human viewable run
                #sleep 0.25
            }
        }

        if { $isdead } {
            set deathlog [open "death.log" w]
            puts $deathlog $expect_out(buffer)
            set send_slow {1 2}
            set doauto 0
            expect {
                -timeout 1
                -re "--More--|(end)" {
                    send -s " "
                    flush stdout
                    sleep 1
                    puts $deathlog $expect_out(buffer)
                    exp_continue
                }
                "yn" {
                    send -s "y"
                    flush stdout
                    sleep 1
                    puts $deathlog $expect_out(buffer)
                    exp_continue
                }
            }
            flush stdout
            close $deathlog
            break
        }
    }
    if { !$isdead && $idle_count >= [llength $dirs] } {
        send "s"
        if { $idle_count > 1000 } {
            # bail...
            set doauto 0
        }
    }
}

if { !$isdead } {
    send_error "XXX: INTERACT\n"
    interact
}

send_error "XXX: clean exit"
